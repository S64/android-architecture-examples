// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext {
        versions = [
            'compile_sdk': 27,
            'min_sdk': 26,
            'target_sdk': 27,
            'build_tools': '27.0.3',
            'aac': '1.1.0',
            'databinding': '3.2.0-alpha06',
            'dagger': '2.15',
            'rxjava': '2.1.10',
            'rxandroid': '2.0.2',
            'kotlin': '1.2.30',
            'support': '27.1.0',
        ]
        app = [
            'version_code': 1,
            'version_name': '0.0.1-SNAPSHOT'
        ]
    }

    repositories {
        google()
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.30"
        classpath 'gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:3.1.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
    tasks.withType(JavaCompile) {
        targetCompatibility = JavaVersion.VERSION_1_8
        sourceCompatibility = JavaVersion.VERSION_1_8
    }
}

subprojects {
    apply plugin: 'org.jlleitschuh.gradle.ktlint'

    ktlint {
        version = '0.14.0'
        android = true
        outputToConsole = true
        ignoreFailures = false
    }

    tasks.whenTaskAdded { task ->
        switch (task.name) {
            case 'assembleDebug':
                task.dependsOn 'ktlintDebugFormat'
                task.dependsOn 'ktlintDebugCheck'
                break
            case 'assembleRelease':
                task.dependsOn 'ktlintReleaseFormat'
                task.dependsOn 'ktlintReleaseCheck'
                break
            case 'assembleDebugAndroidTest':
                task.dependsOn 'ktlintDebugAndroidTestFormat'
                task.dependsOn 'ktlintDebugAndroidTestCheck'
                break
            case 'assembleDebugUnitTest':
                task.dependsOn 'ktlintDebugUnitTestFormat'
                task.dependsOn 'ktlintDebugUnitTestCheck'
                break
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
